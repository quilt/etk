program = _{ SOI ~ "\n"* ~ (stmt ~ "\n"+)* ~ stmt? ~ EOI }

stmt = _{ expr }

expr = _{ jumpdest | op | push }

op = @{ 
	"stop" | "add" | "mul" | "sub" | "div" | "sdiv" | "mod" | "smod" |
	"addmod" | "mulmod" | "exp" | "signextend" | "lt" | "gt" | "slt" |
	"sgt" | "eq" | "iszero" | "and" | "or" | "xor" | "not" | "shl" | "shr" |
	"sar" | "sha3" | "address" | "balance" | "origin" | "caller" |
	"callvalue" | "calldataload" | "calldatasize" | "calldatacopy" |
	"codesize" | "codecopy" | "gasprice" | "extcodesize" | "extcodecopy" |
	"returndatasize" | "returndatacopy" | "extcodehash" | "blockhash" |
	"coinbase" | "timestamp" | "number" | "difficulty" | "gaslimit" | 
	"pop" | "mload" | "mstore" | "mstore8" | "sload" | "sstore" | "jumpi" |
	"jump" | "pc" | "msize" | "gas" | swap | dup | log |
	"create" | "call" | "callcode" | "return" | "delegatecall" | "create2" |
	"staticcall" | "revert" | "selfdestruct"
}

push = { "push" ~  word_size ~ (imm | selector | label) }
swap = { "swap" ~ half_word_size }
dup  = { "dup" ~ half_word_size }
log = { "log" ~ '0'..'4' }

word_size = @{ ('1'..'2' ~ '0'..'9') | ("3" ~ '0'..'2') | '1'..'9' }
half_word_size = @{ ("1" ~ '0'..'6') | '1'..'9' }
imm = @{ (ASCII_HEX_DIGIT)+ }
selector = { "selector(\"" ~ function_declaration ~ "\")" }
function_declaration = { ASCII_ALPHANUMERIC+ ~ "(" ~ ASCII_ALPHANUMERIC* ~ ("," ~ ASCII_ALPHANUMERIC+)* ~ ")" }
	
label = @{ "." ~ (ASCII_ALPHA_LOWER ~ "_"*)+ }
jumpdest = { "jumpdest" ~ label }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ ";" ~ (!"\n" ~ ANY)* }
